## Import the ReactionDiffusion library.
using ReactionDiffusion, WGLMakie
WGLMakie.activate!(resize_to=:body) # Make plots fill the available space.

## Define a reaction network.
reaction = @reaction_network begin
    anterior * μ_bcd,           ∅ --> BCD # Bicoid expression in anterior region
    posterior * μ_nos,          ∅ --> NOS  # Nanos expression in posterior region
    δ_bcd,                      BCD --> ∅   # Bicod degredation
    δ_nos,                      NOS --> ∅ # Nanos degredation
    hillar(BCD, NOS, μ_hb,K,n), ∅ --> HB  # Hunchback expression. Hill function with BCD activating and NOS inhibiting.
    δ_hb,                       HB --> ∅ # Hunchback degredation 
end

## Define a system of diffusing species on a 1D domain of size `L`.
diffusion = @diffusion_system L begin
    D_bcd,  BCD
    D_nos,  NOS
    D_hb,   HB
end

## Combine `reaction` and `diffusion` into a single `Model` object.
model = Model(reaction, diffusion)

## Pick some parameter sets to test.
params = dict(
    L = 2.0,
    # `anterior` and `posterior` are defined as step functions in space. 
    anterior = <(1/12),   # 1 in the anterior 12th of the domain, 0 elsewhere.
    posterior = >(11/12), # 1 in the posterior 12th of the domain, 0 elsewhere.
    μ_bcd = 1.0,
    μ_nos = 1.0,
    δ_bcd = 1.0,
    δ_nos = 1.0,
    μ_hb = 1.0,
    K = 1.0,
    n = 1.0,
    δ_hb = 1.0,
    D_bcd = 1.0,
    D_nos = 1.0,
    D_hb = 0.1
)

## Simulate the system with one of the "good" parameter sets and plot the results over time. 
timeseries_plot(model, params; abstol=1e-3)

## Define some plausible ranges of parameter values to explore.
param_ranges = dict(
    L = range(1.0, 50.0, 50),
    anterior = [x -> (x < 1/12) ? 1 : 0],
    posterior = [x -> (x > 11/12) ? 1 : 0],
    μ_bcd = range(0.1, 2.0, 50),
    μ_nos = range(0.1, 2.0, 50),
    δ_bcd = range(0.1, 2.0, 50),
    δ_nos = range(0.1, 2.0, 50),
    μ_hb = range(0.1, 2.0, 50),
    K = range(0.1, 2.0, 50),
    n = range(1.0, 8.0, 50),
    δ_hb = range(0.1, 2.0, 50),
    D_bcd = range(0.1, 2.0, 50),
    D_nos = range(0.1, 2.0, 50),
    D_hb = range(0.1, 2.0, 50)
)

## Create an interactive plot with sliders to change each parameter.
interactive_plot(model, param_ranges)